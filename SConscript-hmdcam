import os
import platform
import sys
Import('env')

# Building on Tegra; build hmdcam application
tegraEnv = env.Clone()

# Import locally built Monado static libraries
MONADO_BASE='#monado'
MONADO_BUILD='#build/monado'

if not os.path.exists(str(Dir(MONADO_BUILD))):
  sys.exit('Monado build artifacts are not present. Please run ./build-monado.sh first.')

tegraEnv.Append(
  CPPPATH=[
    'hmdcam',
    MONADO_BUILD + '/src/xrt/include', # Contains generated config headers
    MONADO_BASE + '/src/xrt/include',
    MONADO_BASE + '/src/xrt/auxiliary',
  ]
)
monadoStatics = \
    Glob(MONADO_BUILD + '/src/xrt/targets/common/*.a') + \
    Glob(MONADO_BUILD + '/src/xrt/state_trackers/prober/*.a') + \
    Glob(MONADO_BUILD + '/src/xrt/drivers/*.a') + \
    Glob(MONADO_BUILD + '/src/xrt/state_trackers/oxr/*.a') + \
    [
      MONADO_BUILD + '/src/xrt/auxiliary/libaux_os.a',
      MONADO_BUILD + '/src/xrt/auxiliary/libaux_util.a',
      MONADO_BUILD + '/src/xrt/auxiliary/libaux_vive.a',
      MONADO_BUILD + '/src/xrt/auxiliary/libaux_tracking.a', # Needs to be after libaux_vive.a
      MONADO_BUILD + '/src/xrt/auxiliary/libaux_math.a', # Needs to be after libaux_tracking.a for symbol resolution.
      MONADO_BUILD + '/src/xrt/auxiliary/bindings/libaux_generated_bindings.a' # Needs to be after libaux_util.a
    ]


liveMediaStatics = [
  '#live555/libliveMedia.a',
  '#live555/libgroupsock.a',
  '#live555/libBasicUsageEnvironment.a',
  '#live555/libUsageEnvironment.a']

tegraEnv.Program(
  target = '../bin/hmdcam', # relative to build/hmdcam
  source = Glob('hmdcam/*.cpp') + Glob('hmdcam/*.c') + Glob('hmdcam/*.cu') + Glob('common/*.cpp') + Glob('rdma/*.cpp') + Glob('rhi/*.cpp') + Glob('rhi/cuda/*.cpp') + Glob('rhi/gl/*.cpp') + Glob('rhi/egl/*.cpp') + Glob('imgui/*.cpp') + Glob('implot/*.cpp') + Glob('tegra_mmapi/*.cpp') + liveMediaStatics + monadoStatics,
  LIBS=['z', 'stdc++', 'boost_chrono', 'boost_system', 'boost_thread', 'GLESv2', 'EGL', 'drm', 'dl', 'pthread', 'rt', 'hidapi-libusb', 'usb-1.0', 'udev', 'nvargus', 'nvbuf_utils', 'v4l2', 'opencv_core', 'opencv_imgproc', 'opencv_calib3d', 'opencv_aruco', 'opencv_cudaimgproc', 'opencv_cudawarping', 'cuda', 'cudart', 'nppc', 'nppidei', 'ibverbs', 'rdmacm', 'epoxy']
)

